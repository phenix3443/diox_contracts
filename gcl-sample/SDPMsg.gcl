import SDPLib;
import IAuthMessage;
import IContractUsingSDP;
import ISDPMessage;
import ISubProtocol;
import Utils;
import AuthMsg;

contract SDPMsg implements ISDPMessage.SDPMessageInterface, ISubProtocol.SubProtocolInterface {

    @global address owner;

    @global uint64 amContractId;
    @global address amAddress;
    @global array<uint8> localDomain;

    @address map<hash, uint32> sendSeq;
    // map<hash, uint32> recvSeq;

    const uint32 UNORDERED_SEQUENCE = 0xffffffffu32;

    // @notice only for orderred msg
    const uint64 MAX_NONCE = 0xffffffffffffffffu64;

    @global function on_deploy(address _owner) {
        owner = _owner;
        // 自动获取 AuthMsg 的合约 ID 和地址
        amContractId = AuthMsg.__id();
        amAddress = AuthMsg.__address();

        __debug.print("[on_deploy] SDPMsg deployed - address:", __address(), " id:", __id());
        __debug.print("[on_deploy] AM Contract auto-configured - contractId:", amContractId, " address:", amAddress);

        // 自动向 AuthMsg 注册自己 (protocolType=3)
        uint64 selfId = __id();
        address selfAddress = __address();
        uint32 protocolType = 3u32;
        AuthMsg.setProtocol(selfId, selfAddress, protocolType);
        __debug.print("[on_deploy] Registered to AuthMsg - protocolType:", protocolType);
    }

    // 注意：AM 合约已在 on_deploy 中自动配置
    // 此函数仅用于在需要时重新配置（如 AM 合约升级）
    @address function setAmContract(uint64 _amContractId, address _amAddress) public export {
        __debug.assert(__transaction.get_sender() == owner);
        relay@global (^_amContractId, ^_amAddress) {
            amContractId = _amContractId;
            amAddress = _amAddress;
            __debug.print("[setAmContract] AM Contract reconfigured - contractId: ", amContractId, " address: ", amAddress);
        }
    }

    @address function setLocalDomain(array<uint8> domain) public export {
        __debug.assert(__transaction.get_sender() == owner);
        relay@global (^domain) {
            localDomain = domain;
            __debug.print("setLocalDomain: ", localDomain);
        }
    }

    @address function sendMessage(array<uint8> receiverDomain, array<uint8> receiverID, array<uint8> message, uint64 senderID) public export {
        SDPLib.SDPMessage sdpMessage;
        sdpMessage.receiverDomain = Utils.bytesCopy(receiverDomain);
        sdpMessage.receiver = Utils.bytesCopy(receiverID);
        sdpMessage.message = Utils.bytesCopy(message);
        sdpMessage.sequence = _getAndUpdateSendSeq(receiverDomain, senderID, receiverID);

        array<uint8> rawMsg = SDPLib.encodeSDPMsgV1(sdpMessage);
        __debug.print("[sendMessage] rawMsg: ", rawMsg);

        // 调用 AuthMsg.recvFromProtocol
        relay@global (^amContractId, ^amAddress, ^senderID, ^rawMsg) {
            IAuthMessage.AuthMessageInterface am = IAuthMessage.AuthMessageInterface(amContractId);
            am.recvFromProtocol(senderID, rawMsg);
        }
    }

    @address function sendUnorderedMessage(array<uint8> receiverDomain, array<uint8> receiverID, array<uint8> message, uint64 senderID) public export {
        SDPLib.SDPMessage sdpMessage;
        sdpMessage.receiverDomain = Utils.bytesCopy(receiverDomain);
        sdpMessage.receiver = Utils.bytesCopy(receiverID);
        sdpMessage.message = Utils.bytesCopy(message);
        sdpMessage.sequence = UNORDERED_SEQUENCE;

        array<uint8> rawMsg = SDPLib.encodeSDPMsgV1(sdpMessage);
        __debug.print("[sendUnorderedMessage] rawMsg: ", rawMsg);

        // 调用 AuthMsg.recvFromProtocol
        relay@global (^amContractId, ^amAddress, ^senderID, ^rawMsg) {
            IAuthMessage.AuthMessageInterface am = IAuthMessage.AuthMessageInterface(amContractId);
            am.recvFromProtocol(senderID, rawMsg);
        }
    }

    @address function recvMessage(array<uint8> senderDomain, array<uint8> senderID, array<uint8> pkg) public export {
        // __debug.assert(__transaction.get_sender() == amAddress);
        // only SDPv1 now
        // uint32 version = SDPLib.getSDPVersionFrom(pkg);
        relay@global (^senderDomain, ^senderID, ^pkg) {
            _processSDPv1(senderDomain, senderID, pkg);
        }
    }

    @global function _processSDPv1(array<uint8> senderDomain, array<uint8> senderID, array<uint8> pkg) public {
        SDPLib.SDPMessage sdpMessage = SDPLib.decodeSDPMsgV1(pkg);
        __debug.print("sdpMessage: ", sdpMessage);

        __debug.assert(Utils.bytesEqual(sdpMessage.receiverDomain, localDomain));

        if (sdpMessage.sequence == UNORDERED_SEQUENCE) {
            _routeUnorderedMessage(senderDomain, senderID, sdpMessage);
        } else {
            _routeOrderedMessage(senderDomain, senderID, sdpMessage);
        }
    }

    @global function _routeUnorderedMessage(array<uint8> senderDomain, array<uint8> senderID, SDPLib.SDPMessage sdpMessage) public {
        // 从 receiver bytes 中获取接收合约的地址
        address receiver = Utils.arrayUint8ToAddress(sdpMessage.receiver);
        __debug.print("[_routeUnorderedMessage] receiver: ", receiver);

        // 调用接收合约的 recvUnorderedMessage
        IContractUsingSDP.ContractUsingSDPInterface dapp = IContractUsingSDP.ContractUsingSDPInterface(receiver);
        dapp.recvUnorderedMessage(senderDomain, senderID, sdpMessage.message);
    }

    @global function _routeOrderedMessage(array<uint8> senderDomain, array<uint8> senderID, SDPLib.SDPMessage sdpMessage) public {
        // uint32 seqExpected = _getAndUpdateRecvSeq(senderDomain, senderID, sdpMessage.receiver);
        // __debug.assert(sdpMessage.sequence == seqExpected);

        address receiver = Utils.arrayUint8ToAddress(sdpMessage.receiver);
        __debug.print("[_routeOrderedMessage] receiver: ", receiver);

        // 调用接收合约的 recvMessage
        IContractUsingSDP.ContractUsingSDPInterface dapp = IContractUsingSDP.ContractUsingSDPInterface(receiver);
        dapp.recvMessage(senderDomain, senderID, sdpMessage.message);

        // relay@external receiveMessage(senderDomain, senderID, receiver, seqExpected, true, "");
    }


    @global function address getAmAddress() export const {
        return amAddress;
    }

    @global function array<uint8> getLocalDomain() export const {
        return localDomain;
    }

    @global function uint32 querySDPMessageSeq(array<uint8> senderDomain, array<uint8> senderID, array<uint8> receiverDomain, array<uint8> receiverID) export const {
        // 构造序列号查询的键 - 简化实现
        return 0u32;
    }

    @address function uint32 _getAndUpdateSendSeq(array<uint8> receiverDomain, uint64 senderID, array<uint8> receiver) public {
        hash seqKey = SDPLib.getSendingSeqID(receiverDomain, senderID, receiver);
        uint32 seq = sendSeq[seqKey];
        sendSeq[seqKey]++;
        return seq;
    }

    // @global function uint32 _getAndUpdateRecvSeq(array<uint8> senderDomain, array<uint8> sender, array<uint8> receiver) {
    //     hash seqKey = SDPLib.getReceivingSeqID(senderDomain, sender, receiver);
    //     uint32 seq = recvSeq[seqKey];
    //     recvSeq[seqKey]++;
    //     return seq;
    // }

}